program > declaration*

declaration > (type_declaration | binding)

type_declaration > DATA_TYPE EQUALS constructor

constructor > DATA_TYPE fields?

fields > LEFT_PAREN parameter (COMMA parameter)* RIGHT_PAREN

binding > (function | constant) EQUALS expression

function > IDENTIFIER LEFT_PAREN parameters? RIGHT_PAREN COLON type_annotation

parameters > parameter (COMMA parameter)*

parameter > IDENTIFIER COLON type_annotation

type_annotation > BASE_TYPE

constant > IDENTIFIER

expression > logic_or

logic_or > logic_and (OR logic_and)*

logic_and > equality (AND equality)*

equality > addition ((EQUALITY | INEQUALITY) addition)*

addition > multiplication ((PLUS | MINUS) multiplication)*

multiplication > call (TIMES call)*

call > primary (LEFT_PAREN arguments? RIGHT_PAREN)*

arguments > expression (COMMA expression)*

primary
    > LET binding* IN expression
    | IF expression THEN expression ELSE expression
    | LEFTPAREN expression RIGHTPAREN
    | LITERAL
    | IDENTIFIER
